plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
}

group 'com.rundeck'

ext.rundeckPluginVersion = '1.2'
ext.pluginClassNames=
        'com.rundeck.plugins.azure.plugin.AzureResourceModelSourceFactory,com.rundeck.plugins.azure.plugin.AzureFileStoragePlugin,com.rundeck.plugins.azure.plugin.AzureStorageCopyStepPlugin,com.rundeck.plugins.azure.plugin.AzureStorageListStepPlugin,com.rundeck.plugins.azure.plugin.AzureStorageDeleteStepPlugin,com.rundeck.plugins.azure.plugin.AzureVMCreatePlugin,com.rundeck.plugins.azure.plugin.AzureVmStartPlugin,com.rundeck.plugins.azure.plugin.AzureVmStopPlugin,com.rundeck.plugins.azure.plugin.AzureVmListPlugin'
ext.pluginName = 'Azure Rundeck Plugin'
ext.pluginDescription = 'Azure Plugin integrates Rundeck with Azure Virtual Machines and Azure Storage. The plugin contains a Resource Model plugin, an Execution Log Storage plugin, and others plugin steps like Create/Start/Stop Azure VMs.'

apply plugin: 'groovy'
apply plugin: 'java'

sourceCompatibility = 1.8

scmVersion {
    tag {
        prefix = ''
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}
project.version = scmVersion.version

repositories {
    mavenCentral()
    mavenLocal()
}



configurations {
    pluginLibs

    compile {
        extendsFrom pluginLibs
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.rundeck', name: 'rundeck-core', version: '2.10.0'
    pluginLibs (group: 'com.microsoft.azure', name: 'azure', version: '1.3.0'){
        exclude group: "com.fasterxml.jackson.core"
    }
    pluginLibs (group: 'com.microsoft.azure', name: 'azure-storage', version: '6.1.0') {
        exclude group: "com.fasterxml.jackson.core"
    }
    pluginLibs group: 'commons-net', name: 'commons-net', version: '3.5'
    pluginLibs group: 'commons-io', name: 'commons-io', version: '2.5'

    // https://mvnrepository.com/artifact/com.microsoft.azure/azure-keyvault-core
    pluginLibs group: 'com.microsoft.azure', name: 'azure-keyvault-core', version: '1.0.0'


    testCompile "org.codehaus.groovy:groovy-all:2.3.7"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "cglib:cglib-nodep:2.2.2"
    testCompile 'org.objenesis:objenesis:1.4'

    constraints {
        pluginLibs("com.squareup.retrofit2:adapter-rxjava:2.6.1")  {
            because "retrofit version by azure affected by CVE-2018-1000844"
        }
    }

}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name' : pluginName
        attributes 'Rundeck-Plugin-Description' : pluginDescription
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.8.2+'
        attributes 'Rundeck-Plugin-Tags': 'java,executor file storage,resource model,workflow step,azure'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/rundeck-azure-plugin'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': project.version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
    dependsOn(copyToLib)
}

